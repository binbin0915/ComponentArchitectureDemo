apply plugin: getModelType()
apply plugin: "kotlin-android"
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.kotlin.android'

String getModelType() {
    if (isRunModel.toBoolean()) {
        return "com.android.application"
    } else {
        return "com.android.library"
    }
}

android {
    compileSdk 33
    defaultConfig {
        minSdk 21
        targetSdk 33
        versionCode 2
        versionName "1.0"
    }
    /*构建类型*/
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'IS_RUN_MODEL', "${isRunModel.toBoolean()}"
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'IS_RUN_MODEL', "${isRunModel.toBoolean()}"
        }
    }

    sourceSets {
        main {
            if (isRunModel.toBoolean()) {
                //独立运行
                manifest.srcFile 'src/main/alone/AndroidManifest.xml'
            } else {
                //合并到宿主
                manifest.srcFile 'src/main/AndroidManifest.xml'
                resources {
                    //正式版本时，排除debug文件夹下所有调试文件
                    exclude 'src/debug/*'
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        // 构建指定 jvm 版本
        jvmTarget = '11'
    }

    composeOptions {
        // Compose 是 Kotlin 开发的，需要配置 Kotlin 对应的编译扩展版本
        kotlinCompilerExtensionVersion '1.4.0-dev-k1.7.20-e49b3b6028b'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    defaultConfig {
        vectorDrawables {
            useSupportLibrary true
        }
    }
    viewBinding {
        enabled = true
    }
}
//模块路由配置
kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}
dependencies {
    //路由
    implementation('com.alibaba:arouter-api:1.5.2') {
        //过滤掉v4包里面的东西 防止和AndroidX冲突
        exclude group: 'com.android.support', module: 'support-v4'
    }
    kapt 'com.alibaba:arouter-compiler:1.5.2'    //ARouter路由compiler

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:latest.release'
    implementation 'com.google.android.material:material:latest.release'

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    //KTX
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-service:2.4.1'

    //组件包
    implementation project(":library:lib-widget")
    //公共基础包
    implementation project(":library:lib-common")


    //-------------------------------------jetpack compose开始---------------------------------------
    implementation 'androidx.compose.ui:ui:1.2.1'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.2.1'
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation 'androidx.compose.foundation:foundation:1.2.1'
    // Material Design
    implementation 'androidx.compose.material:material:1.2.1'
    // Material design icons
    implementation 'androidx.compose.material:material-icons-core:1.2.1'
    implementation 'androidx.compose.material:material-icons-extended:1.2.1'
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.5.1'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    // Integration with observables
    implementation 'androidx.compose.runtime:runtime-livedata:1.2.1'
    implementation 'androidx.compose.runtime:runtime-rxjava2:1.2.1'
    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.2.1'

    debugImplementation "androidx.compose.ui:ui-tooling:1.2.1"
    implementation "androidx.compose.ui:ui-tooling-preview:1.2.1"

    // When using a MDC theme
    implementation "com.google.android.material:compose-theme-adapter:1.1.16"
    // When using a AppCompat theme
    implementation "com.google.accompanist:accompanist-appcompat-theme:0.25.1"
    //-------------------------------------jetpack compose结束---------------------------------------
}